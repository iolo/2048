10 REM 2048 IN APPLESOFT BASIC
20 REM ADAPTED FROM PYTHON VERSION

100 REM -- INITIALIZATION --
110 BH = 4 : BW = 4: DIM B(BH,BW) : REM 4X4 BOARD, 0-INDEXED
120 GT = 13 : REM GOAL TILE 13 FOR 2^13=8192
130 GO = 0 : REM 2=WIN, 1=GAME OVER(NO MOVES), 0=OTHERWISE
150 CH = 0 : REM CHANGED FLAG AFTER MERGE
160 DIM E(BH*BW,1) : REM EMPTY CELLS
170 DIM T(BH+BW) : REM TEMP FOR SLIDE AND MERGE

200 REM -- INIT BOARD WITH FIRST TWO TILES --
210 GOSUB 1000: REM INIT BOARD
220 GOSUB 2000 : REM ADD A NEW TILE
230 GOSUB 2000 : REM ADD A NEW TILE

300 REM -- MAIN GAME LOOP --
310 GOSUB 12000 : REM DISPLAY BOARD
312 GOSUB 8000 : REM CHECK GAME OVER
313 IF GO = 2 THEN GOTO 19000: REM WIN!
314 IF GO = 1 THEN GOTO 18000: REM GAME OVER
320 GOSUB 15000 : REM GET USER INPUT
350 IF A$ = "Q" OR A$=CHR$(27) THEN GOTO 17000
355 CH=0: REM CLEAR CHANGES FLAG
360 IF A$ = "W" OR A$=CHR$(11) THEN GOSUB 3000 : REM MOVE UP
370 IF A$ = "A" OR A$=CHR$(8) THEN GOSUB 4000 : REM MOVE LEFT
380 IF A$ = "S" OR A$=CHR$(10) THEN GOSUB 5000 : REM MOVE DOWN
390 IF A$ = "D" OR A$=CHR$(21) THEN GOSUB 6000 : REM MOVE RIGHT
395 IF CH = 1 THEN GOSUB 2000 : REM ADD A NEW TILE
396 IF CH = 0 THEN PRINT CHR$(7) : REM NO CHANGES
400 GOTO 310 : REM LOOP

1000 REM -- INITIALIZE BOARD TO ZEROS --
1010 FOR Y = 0 TO BH-1
1020     FOR X = 0 TO BW-1
1030         B(Y,X) = 0
1040     NEXT X
1050 NEXT Y
1060 RETURN

2000 REM -- ADD NEW TILE --
2010 REM FIND EMPTY CELLS
2030 EC = 0
2040 FOR Y = 0 TO BH-1
2050     FOR X = 0 TO BW-1
2060         IF B(Y,X) = 0 THEN E(EC,0)=Y: E(EC,1)=X: EC=EC+1
2070     NEXT X
2080 NEXT Y
2090 IF EC = 0 THEN RETURN : REM NO EMPTY SPOTS
2100 REM CHOOSE A RANDOM EMPTY CELL
2110 I = INT (RND(1) * EC): Y = E(I,0): X = E(I,1)
2120 REM SET TILE TO 2 OR 4 (90% CHANCE OF 2)
2130 IF RND(1) < 0.9 THEN B(Y,X) = 1 : RETURN : REM 1 FOR 2^1=2
2140 B(Y,X) = 2 : REM 2 FOR 2^2=4
2150 RETURN

3000 REM -- UP --
3010 FOR X=0 TO BW-1
3020   I=0
3030   FOR Y=0 TO BH-1
3040     T(Y)=0
3050     IF B(Y,X)=0 THEN GOTO 3100: REM SKIP ZERO
3060     CH=1
3070     IF B(Y,X)=T(I) THEN T(I)=T(I)+1: I=I+1: GOTO 3100: REM MERGE
3080     IF T(I)=0 THEN T(I)=B(Y,X): GOTO 3100: REM SLIDE
3090     I=I+1: T(I)=B(Y,X)
3100   NEXT Y
3110   FOR Y=0 TO BH-1: B(Y,X)=T(Y): NEXT Y
3120 NEXT X
3140 RETURN

4000 REM -- LEFT --
4010 FOR Y=0 TO BH-1
4020   I=0
4030   FOR X=0 TO BW-1
4040     T(X)=0
4050     IF B(Y,X)=0 THEN GOTO 4100: REM SKIP ZERO
4060     CH=1
4070     IF B(Y,X)=T(I) THEN T(I)=T(I)+1: I=I+1: GOTO 4100: REM MERGE
4080     IF T(I)=0 THEN T(I)=B(Y,X): GOTO 4100: REM SLIDE
4090     I=I+1: T(I)=B(Y,X)
4100   NEXT X
4110   FOR X=0 TO BW-1: B(Y,X)=T(X): NEXT X
4120 NEXT Y
4140 RETURN

5000 REM -- DOWN --
5010 FOR X=0 TO BW-1
5020   I=BH-1
5030   FOR Y=BH-1 TO 0 STEP -1
5040     T(Y)=0
5050     IF B(Y,X)=0 THEN GOTO 5100: REM SKIP ZERO
5060     CH=1
5070     IF B(Y,X)=T(I) THEN T(I)=T(I)+1: I=I-1: GOTO 5100: REM MERGE
5080     IF T(I)=0 THEN T(I)=B(Y,X): GOTO 5100: REM SLIDE
5090     I=I-1: T(I)=B(Y,X)
5100   NEXT Y
5110   FOR Y=0 TO BH-1: B(Y,X)=T(Y): NEXT Y
5120 NEXT X
5140 RETURN

6000 REM -- RIGHT --
6010 FOR Y=0 to BH-1
6020   I=BW-1
6030   FOR X=BW-1 TO 0 STEP -1
6040     T(X)=0
6050     IF B(Y,X)=0 THEN GOTO 6100: REM SKIP ZERO
6060     CH=1
6070     IF B(Y,X)=T(I) THEN T(I)=T(I)+1: I=I-1: GOTO 6100: REM MERGE
6080     IF T(I)=0 THEN T(I)=B(Y,X): GOTO 6100: REM SLIDE
6090     I=I-1: T(I)=B(Y,X)
6100   NEXT X
6110   FOR X=0 TO BW-1: B(Y,X)=T(X): NEXT X
6120 NEXT Y
6140 RETURN

8000 REM -- CHECK GAME OVER --
8010 REM 2=WIN, 1=GAME OVER(NO MOVES), 0=OTHERWISE
8030 FOR Y = 0 TO BH-1
8040     FOR X = 0 TO BW-1
8045         REM CHECK FOR WINNING TILE
8050         IF B(Y,X) = GT THEN GO = 2: RETURN
8055         REM CHECK FOR ANY EMPTY CELLS
8060         IF B(Y,X) = 0 THEN GO = 0: RETURN
8070         REM CHECK FOR HORIZONTAL MERGES
8080         IF X>0 THEN IF B(Y,X) = B(Y,X - 1) THEN GO = 0: RETURN
8090         REM CHECK FOR VERTICAL MERGES
8100         IF Y>0 THEN IF B(Y,X) = B(Y - 1,X) THEN GO = 0: RETURN
8110     NEXT X
8120 NEXT Y
8125 GO = 1: REM NO MOVES - GAME OVER
8130 RETURN

12000 REM -- DISPLAY BOARD(TEXT MODE) --
12010 HOME
12020 FOR X = 0 TO BW-1: PRINT "+----"; : NEXT X: PRINT "+"
12030 FOR Y = 0 TO BH-1
12040     PRINT "|";
12050     FOR X = 0 TO BW-1
12060         IF B(Y,X) = 0 THEN PRINT "    |"; : GOTO 12100
12070         N$ = STR$(2^B(Y,X))
12080         PRINT N$; SPC(4 - LEN(N$)); "|";
12100     NEXT X
12110     PRINT
12125     FOR X = 0 TO BW-1: PRINT "+----"; : NEXT X: PRINT "+"
12140 NEXT Y
12145 PRINT
12150 RETURN

15000 REM -- GET USER INPUT --
15010 PRINT "W/A/S/D/Q: ";
15020 GET A$
15030 PRINT A$ : REM ECHO THE KEYPRESS
15040 RETURN

17000 REM -- QUIT --
17010 PRINT "QUIT"
17020 END

18000 REM -- GAME OVER --
18010 PRINT "GAME OVER"
18020 END

19000 REM -- WIN --
19010 PRINT "YOU WIN!"
19020 END
